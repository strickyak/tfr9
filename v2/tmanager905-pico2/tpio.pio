; tpio.pio -- for the TFR/901 -- strick
; This is the King's PIO assembly language code.
;
; SPDX-License-Identifier: MIT

; E = 1
; Q = 2

; Phase1 : 0 : neither
; Phase2 : 2 : just Q
; Phase3 : 3 : both
; Phase4 : 1 : just E

;  NEW: 12: CounterReset & CounterClock inactive
;  NEW: Subtract 4:   CounterClock active
;  NEW: Subtract 8:   CounterReset active

.program tpio
.side_set 4

   mov y, ~null     side 12 ; Phase1 + Neutral  ;  Y := $FFFFFFFF

.wrap_target                  ; BEGIN LOOP
top:
    nop     [1]     side  4 ; Phase1 + CounterReset(-8)  ; Y0: selects A0-7
;;;;;;;;;

    ; WAIT FOR THE GO_AHEAD
; offset `2`
    pull block [1]      side 12 ;

    nop     [1]     side 12 ; Phase1 + Neutral
    ; nop     [0]     side 12 ; Phase1 + Neutral

;;;;;;;;;
    in  null, 8     side 12 ; Phase1 + Neutral
    in  pins, 8     side 10 ; Phase2 + CounterClock(-4)  ; Y1: selects A8-15
    nop   [1]       side 14 ; Phase2 + Neutral
    nop   [1]       side 14 ; Phase2 + Neutral
    nop   [0]       side 14 ; Phase2 + Neutral
    in  pins, 8     side 11 ; Phase2 + CounterClock(-4)  ; Y2: selects R/W & status
    nop   [1]       side 15 ; Phase3 + Neutral
    nop   [1]       side 15 ; Phase3 + Neutral
    nop   [0]       side 15 ; Phase3 + Neutral
    in  pins, 8     side 15 ; Phase3 + Neutral   ; Read Status bits (R/W, AVMA, LLIC...)
    jmp pin, read   side 9 ; Phase 4 + Y3

write:                        ; IF WRITE CYCLE (Receiving from CPU)
    nop    [1]      side 13 ; Phase4 + Neutral
    nop    [1]      side 13 ; Phase4 + Neutral
    nop    [0]      side 13 ; Phase4 + Neutral
    in pins, 32     side 13 ; Phase4 + Neutral   ; Receive data.
    jmp top         side 12 ; Phase1 + Neutral 

read:                         ; ELSE READ CYCLE (Sending to CPU)
    pull block      side 13 ; Phase4 + Neutral  ; pulls a 32 bit word, but we only use 24 bits.
    out pindirs,8   side 9 ; Phase4 + CounterClock(-4)  ; change Data Pins to output, and Y4.

    out pins,8      side 13 ; Phase4 + Neutral   ; output the data.
    nop    [1]      side 13 ; Phase4 + Neutral
    nop    [0]      side 13 ; Phase4 + Neutral
    out pindirs,8   side 12 ; Phase1 + Neutral   ; change Data Pins to inputs

.wrap                         ; REPEAT LOOP

% c-sdk {

void tpio_program_init(PIO pio, uint sm, uint offset) {

   // At 150 MHz, CLOCK_DIVISOR can be 2, for 0.710156
   // For 200(0.686792) or 250 or 270(0.509053) MHz, CLOCK_DIVISOR seems to be 3.
   constexpr uint CLOCK_DIVISOR = 3;

   //
   //  pico-examples/pio/hub75/hub75.pio shows order of inits.
   //
   pio_sm_set_consecutive_pindirs(pio, sm, 0, 8, false/*in*/);
   pio_sm_set_consecutive_pindirs(pio, sm, 8, 4, true/*out*/);
   for (uint i = 0; i < 12; i++) {
       pio_gpio_init(pio, i);
   }

   pio_sm_config cf = tpio_program_get_default_config(offset);
   sm_config_set_jmp_pin(&cf, 0);
   sm_config_set_in_pins(&cf, 0);
   sm_config_set_out_pins(&cf, 0, 8);
   sm_config_set_sideset(&cf, 4, false, false);
   sm_config_set_sideset_pins(&cf, 8);
   sm_config_set_clkdiv(&cf, CLOCK_DIVISOR);

   // IN: Shift from left, do autopush
   const bool IN_SHIFT_RIGHT = false;
   const bool AUTOPUSH = true;
   const uint PUSH_THRESHOLD = 32;
   sm_config_set_in_shift(&cf, IN_SHIFT_RIGHT, AUTOPUSH, PUSH_THRESHOLD);

   // OUT: Shift to right, don't autopull
   const bool OUT_SHIFT_RIGHT = true;
   const bool AUTOPULL = false;
   const uint PULL_THRESHOLD = 8;
   sm_config_set_out_shift(&cf, OUT_SHIFT_RIGHT, AUTOPULL, PULL_THRESHOLD);

   pio_sm_init(pio, sm, offset, &cf);
   pio_sm_exec(pio, sm, offset);
   pio_sm_set_enabled(pio, sm, true);
}
%}
